PREFIX bmo: <http://collection.britishmuseum.org/id/ontology/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX nm: <http://nomisma.org/id/>
PREFIX nmo: <http://nomisma.org/ontology#>

SELECT ?subject (group_concat(distinct ?o;separator="; ") as ?objects) ?certainty
WHERE {
    {
        ?subject nmo:hasMint ?o.

        ?b crm:P140_assigned_attribute_to ?subject ;
            crm:P141_assigned ?o ;
            bmo:PX_Property nmo:hasMint ;
            a crm:E13_Attribute_Assignment ;
            bmo:PX_likelihood nm:uncertain_value .
            
        BIND("uncertain" AS ?certainty)
    }
    UNION {
        {
            ?subject nmo:hasMint ?o.
        }
        MINUS {
            ?b crm:P140_assigned_attribute_to ?subject ;
                crm:P141_assigned ?object ;
                bmo:PX_Property nmo:hasMint ;
                a crm:E13_Attribute_Assignment ;
                bmo:PX_likelihood nm:uncertain_value .
        }
        BIND("certain" AS ?certainty)
    }
}
GROUP BY ?subject ?certainty