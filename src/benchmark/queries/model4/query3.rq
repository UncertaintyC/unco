PREFIX crminf: <http://www.cidoc-crm.org/crminf/sites/default/files/CRMinf_v0.7_.rdfs#>
PREFIX nmo: <http://nomisma.org/ontology#>

SELECT ?subject (group_concat(distinct ?o;separator="; ") as ?objects) ?certainty
WHERE {
    {
        ?subject nmo:hasMint ?a .

        ?a crminf:J2_concluded_that ?b ;
            a crminf:I5_Inference_Making .

        ?b crminf:J4_that ?o ;
            crminf:J5_holds_to_be ?certainty ;
            crminf:I4_Proposition_Set ?proposetion ;
            a crminf:I2_Belief .
    }
    UNION {
        {
            ?subject nmo:hasMint ?o.
        }
        MINUS {
            ?o crminf:J2_concluded_that ?b ;
                a crminf:I5_Inference_Making .
                
            ?b crminf:J4_that ?ob ;
                crminf:J5_holds_to_be ?c ;
                crminf:I4_Proposition_Set ?p ;
                a crminf:I2_Belief .
        }
        BIND("certain" AS ?certainty)
    }
}
GROUP BY ?subject ?certainty